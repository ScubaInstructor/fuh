plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
}

group = 'com.vanwaasen.flink'
version = '1.0'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    compileOnly("org.apache.flink:flink-streaming-java:1.20.0")
    implementation("org.apache.flink:flink-connector-kafka:3.4.0-1.20")

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'

    implementation 'org.apache.kafka:kafka-clients:3.7.1'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.vanwaasen.cicfm.flink.HttpFlinkProcessor'
        )
    }
}

tasks.register("fatJar", Jar) {
    archiveBaseName.set("flink-http-sender")
    archiveVersion.set("1.5.0")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes(
                'Main-Class': 'com.vanwaasen.cicfm.flink.HttpFlinkProcessor'
        )
    }

    from {
        configurations.runtimeClasspath.filter { it.exists() }
                .collect { it.isDirectory() ? it : zipTree(it) }
    }

    with tasks.jar
}